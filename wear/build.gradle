plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

apply from: "../base.gradle"

android {
    defaultConfig {
        minSdk project.minSdkVersionWear
        applicationId project.applicationId
    }

    if (canSignRelease) {
        signingConfigs {
            release {
                storeFile = file(pocketcastsKeyStoreFile)
                storePassword = pocketcastsKeyStorePassword
                keyAlias = pocketcastsKeyStoreAlias
                keyPassword = pocketcastsKeyStoreAliasPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (canSignRelease) {
                signingConfig signingConfigs.release
            }
        }
    }

    buildFeatures {
        compose true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        // Allow for widescale experimental APIs in Alpha libraries we build upon
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.ExperimentalHorologistMediaApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.ui.ExperimentalHorologistMediaUiApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.data.ExperimentalHorologistMediaDataApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.composables.ExperimentalHorologistComposablesApi"
    }
}

dependencies {
    // General Compose dependencies
    implementation androidLibs.composeActivity
    implementation androidLibs.composeUi
    implementation androidLibs.composeUiToolingPreview
    implementation libs.hiltNavigationCompose

    // Compose for Wear OS Dependencies
    implementation androidLibs.wearComposeMaterial
    implementation androidLibs.wearComposeFoundation
    implementation androidLibs.wearComposeNavigation

    // Horologist Dependencies
    implementation androidLibs.horologistMedia
    implementation androidLibs.horologistMediaUi
    implementation androidLibs.horologistMediaData

    wearApp project(":wear")

    implementation project(':modules:services:compose')
    implementation project(':modules:services:localization')
    implementation project(':modules:services:preferences')
    implementation project(':modules:services:ui')
    implementation project(':modules:services:utils')
    implementation project(':modules:services:images')
    implementation project(':modules:services:repositories')
    implementation project(':modules:services:model')
    implementation project(':modules:features:profile')
}